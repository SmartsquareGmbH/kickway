import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

buildscript {
    ext {
        gradleVersion = "4.10.2"
        gradleVersionsVersion = "0.20.0"

        kotlinVersion = "1.2.71"
        springBootVersion = "2.0.5.RELEASE"

        detektVersion = "1.0.0.RC9.2"
        ktlintVersion = "0.28.0"
        ktlintGradleVersion = "6.0.0"
        codeNarcVersion = "1.2.1"
        jacocoVersion = "0.8.2"

        jacksonVersion = "2.9.7"

        spockVersion = "1.1-groovy-2.4"

        mockserverVersion = "5.4.1"
    }

    repositories {
        gradlePluginPortal()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"

        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"

        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:$ktlintGradleVersion"
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsVersion"
    }
}

apply plugin: "groovy"
apply plugin: "kotlin"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-jpa"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-spring"

apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "codenarc"
apply plugin: "jacoco"

apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"

group = "de.smartsquare.kickchain"
version = "1.0.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.springframework.boot:spring-boot-starter-web"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation 'org.springframework.data:spring-data-keyvalue'
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    kapt "org.springframework.boot:spring-boot-configuration-processor"

    runtime "org.springframework.boot:spring-boot-devtools"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

    testImplementation "org.mock-server:mockserver-netty:$mockserverVersion"
    testImplementation "org.mock-server:mockserver-client-java:$mockserverVersion"

    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation "org.spockframework:spock-spring:$spockVersion"
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

detekt {
    version = detektVersion

    config = files("$projectDir/config/detekt/detekt.yml")
    input = files("$projectDir/src/main/kotlin")

    reports {
        xml.destination = file("$buildDir/reports/detekt/detekt.xml")
        html.destination = file("$buildDir/reports/detekt/detekt.html")
    }
}

ktlint {
    version = ktlintVersion
    reporters = [ReporterType.CHECKSTYLE]
}

codenarc {
    toolVersion = codeNarcVersion

    configFile = file("$projectDir/config/codenarc/codenarc.xml")
}

gradle.projectsEvaluated {
    test { finalizedBy jacocoTestReport }
}

wrapper {
    gradleVersion = project.gradleVersion
}